/*
BiRow function as defined by Peter Bartholomew
*/
BiRow_byPeterBartholomew =LAMBDA(array,function,LET(\\LambdaName, "BiRow_byPeterBartholomew", \\Description, "Perform array function by row", \\Source, "attributed to Peter Bartholomew: https://techcommunity.microsoft.com/discussions/excelgeneral/recursive-lambda-implementation-of-excels-reduce-function-/3949754/replies/3952653#M207809", IF(ROWS(array) = 1, function(array), VSTACK(BiRow_byPeterBartholomew(TAKE(array, ROWS(array) / 2), function), BiRow_byPeterBartholomew(DROP(array, ROWS(array) / 2), function)))));